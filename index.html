<!DOCTYPE html>
<html lang="pt-br">
	<head>
		<title>Room View</title>
		<link rel="icon" href="https://thumbs.dreamstime.com/b/no-signal-tv-texture-television-grainy-noise-effect-as-background-no-signal-retro-vintage-television-pattern-interfering-no-157996496.jpg">
		<style>
			body { margin: 0; overflow: hidden; }
			canvas { width: 100%; height: 100% }
		</style>
	</head>
	<body>
		<canvas id="mycanvas"></canvas>
			<div>
				<script src="https://unpkg.com/three@0.116.1/build/three.js"></script>
				<script src="https://unpkg.com/three@0.116.1/examples/js/controls/OrbitControls.js"></script>
				<script src="https://unpkg.com/three@0.116.1/examples/js/controls/DragControls.js"></script>
				<script src="https://unpkg.com/three@0.116.1/examples/js/loaders/OBJLoader.js"></script>
				<script src="https://unpkg.com/three@0.116.1/examples/js/loaders/MTLLoader.js"></script>
				<!-- Shaders baseados em https://al-ro.github.io/ -->
				<script id="grassVertexSource" type="x-shader/x-vertex">
					uniform sampler2D noiseTexture;
					float getYPosition(vec2 p) {
				 		return 8.0*(2.0*texture2D(noiseTexture, p/800.0).r - 1.0);
					}
					precision mediump float;
					attribute vec3 position;
					attribute vec3 normal;
					attribute vec3 offset;
					attribute vec2 uv;
					attribute vec2 halfRootAngle;
					attribute float scale;
					attribute float index;
					uniform float time;

					uniform float delta;
					uniform float posX;
					uniform float posZ;
					uniform float radius;
					uniform float width;

					uniform mat4 modelViewMatrix;
					uniform mat4 projectionMatrix;

					varying vec2 vUv;
					varying vec3 vNormal;
					varying vec3 vPosition;
					varying float frc;
					varying float idx;

					//https://www.geeks3d.com/20141201/how-to-rotate-a-vertex-by-a-quaternion-in-glsl/
					vec3 rotateVectorByQuaternion(vec3 v, vec4 q){
					  return 2.0 * cross(q.xyz, v * q.w + cross(q.xyz, v)) + v;
					}

					void main() {

						//Vertex height in blade geometry
						frc = position.y / 1.0;

						//Scale vertices
					  vec3 vPosition = position;
						vPosition.y *= scale;

						//Invert scaling for normals
						vNormal = normal;
						vNormal.y /= scale;

						//Rotate blade around Y axis
					  vec4 direction = vec4(0.0, halfRootAngle.x, 0.0, halfRootAngle.y);
						vPosition = rotateVectorByQuaternion(vPosition, direction);
						vNormal = rotateVectorByQuaternion(vNormal, direction);

					  //UV for texture
					  vUv = uv;

						vec3 pos;
						vec3 globalPos;
						vec3 tile;

						globalPos.x = offset.x-posX*delta;
						globalPos.z = offset.z-posZ*delta;

						tile.x = floor((globalPos.x + 0.5 * width) / width);
						tile.z = floor((globalPos.z + 0.5 * width) / width);

						pos.x = globalPos.x - tile.x * width;
						pos.z = globalPos.z - tile.z * width;
						pos.y = -112.0;

						//Wind is sine waves in time
						float noise = sin(0.1 * pos.x + 0.8*time);
						float halfAngle = noise * 0.1;
						noise = 0.5 + 0.5 * cos(0.05 * pos.x + 2.0 * time);
						halfAngle -= noise * 0.2;

						direction = normalize(vec4(sin(halfAngle), 0.0, -sin(halfAngle), cos(halfAngle)));

						//Rotate blade and normals according to the wind
					  vPosition = rotateVectorByQuaternion(vPosition, direction);
						vNormal = rotateVectorByQuaternion(vNormal, direction);

						//Move vertex to global location
						vPosition += pos;

						//Index of instance for varying colour in fragment shader
						idx = index;

					  gl_Position = projectionMatrix * modelViewMatrix * vec4(vPosition, 1.0);
					}
				</script>
				<script id="grassFragmentSource" type="x-shader/x-vertex">
					precision mediump float;

					uniform vec3 cameraPosition;

					//Light uniforms
					uniform float ambientStrength;
					uniform float diffuseStrength;
					uniform float specularStrength;
					uniform float translucencyStrength;
					uniform float shininess;
					uniform vec3 lightColour;
					uniform vec3 sunDirection;

					//Surface uniforms
					uniform sampler2D map;
					uniform sampler2D alphaMap;
					uniform vec3 specularColour;

					varying float frc;
					varying float idx;
					varying vec2 vUv;
					varying vec3 vNormal;
					varying vec3 vPosition;

					vec3 ACESFilm(vec3 x){
						float a = 2.51;
						float b = 0.03;
						float c = 2.43;
						float d = 0.59;
						float e = 0.14;
						return clamp((x*(a*x+b))/(x*(c*x+d)+e), 0.0, 1.0);
					}

					void main() {
					  //If transparent, don't draw
					  if(texture2D(alphaMap, vUv).r < 0.15){
					    discard;
					  }

						vec3 normal;

						//Flip normals when viewing reverse of the blade
						if(gl_FrontFacing){
							normal = normalize(vNormal);
						}else{
							normal = normalize(-vNormal);
						}

					  //Get colour data from texture
						vec3 textureColour = pow(texture2D(map, vUv).rgb, vec3(2.2));

					  //Add different green tones towards root
						vec3 mixColour = idx > 0.75 ? vec3(0.07, 0.52, 0.06) : vec3(0.07, 0.43, 0.08);
					  textureColour = mix(pow(mixColour, vec3(2.2)), textureColour, frc);

						vec3 lightTimesTexture = lightColour * textureColour;
					  vec3 ambient = textureColour;
						vec3 lightDir = normalize(sunDirection);

					  //How much a fragment faces the light
						float dotNormalLight = dot(normal, lightDir);
					  float diff = max(dotNormalLight, 0.0);

					  //Colour when lit by light
					  vec3 diffuse = diff * lightTimesTexture;

					  float sky = max(dot(normal, vec3(0,1,0)), 0.0);
						vec3 skyLight = sky * vec3(0.12, 0.29, 0.55);

					  vec3 viewDirection = normalize(cameraPosition - vPosition);
					  vec3 halfwayDir = normalize(lightDir + viewDirection);
					  //How much a fragment directly reflects the light to the camera
					  float spec = pow(max(dot(normal, halfwayDir), 0.0), shininess);

					  //Colour of light sharply reflected into the camera
					  vec3 specular = spec * specularColour * lightColour;

						//https://en.wikibooks.org/wiki/GLSL_Programming/Unity/Translucent_Surfaces
						vec3 diffuseTranslucency = vec3(0);
						vec3 forwardTranslucency = vec3(0);
						float dotViewLight = dot(-lightDir, viewDirection);
						if(dotNormalLight <= 0.0){
							diffuseTranslucency = lightTimesTexture * translucencyStrength * -dotNormalLight;
							if(dotViewLight > 0.0){
								forwardTranslucency = lightTimesTexture * translucencyStrength * pow(dotViewLight, 16.0);
							}
						}

					  vec3 col = 0.3 * skyLight * textureColour + ambientStrength * ambient + diffuseStrength * diffuse + specularStrength * specular + diffuseTranslucency + forwardTranslucency;

					  //Tonemapping
					  col = ACESFilm(col);

					  //Gamma correction 1.0/2.2 = 0.4545...
						col = pow(col, vec3(0.4545));

						//Add a shadow towards root
						col = mix(vec3(0.0, 0.2, 0.0), col, frc);

					  gl_FragColor = vec4(col, 1.0);
					}
				</script>
				<script id="vertexVideoShader" type="x-shader/x-vertex">
					varying vec2 vUv;

					void main() {
					  vUv = uv;
					  gl_Position = projectionMatrix*modelViewMatrix*vec4(position, 1.0);
					}
				  </script>
				  <script id="fragmentVideoShader" type="x-shader/x-fragment">
					  uniform sampler2D u_tDiffuse;
					  uniform vec2 u_resolution;
					  uniform float u_time;
					  uniform float u_amount;
					  uniform float u_angle;
					  uniform float u_magnitude;

					  varying vec2 vUv;

					  float randomHoriz(vec2 st) {
						  return fract(sin(dot(st.xy, vec2(0,10000.0)))*43758.5453123)/8.0;
					  }

					  float random (vec2 st) {
						  return fract(sin(dot(st.xy ,vec2(12.9898,78.233))) * 43758.5453)/2.0;
					  }

					  void main() {
							// armazena um sistema de coordenadas normalizado
							vec2 st = gl_FragCoord.xy/u_resolution.xy;

							// multiplica a escala do sistema de cordenadas por u_amount
							st *= u_amount;

							// pega as coordenadas inteiras do sistema para criar um grid
							vec2 ipos = floor(st);

							// atribui um valor aleatório com base nas coordenadas inteiras
							float noise = randomHoriz(ipos*u_time);

							// desloca o sistema de coordenadas das cores vermelho e azul
							float rdn = random(ipos*u_time);
							vec2 offset = u_magnitude*(1.0+rdn)*vec2( cos(u_angle*(1.0+rdn)), sin(u_angle*(1.0+rdn)));
							vec4 red = texture2D(u_tDiffuse, vUv + offset);
							vec4 green = texture2D(u_tDiffuse, vUv);
							vec4 blue = texture2D(u_tDiffuse, vUv - offset);

							gl_FragColor = vec4(red.r - noise, green.g - noise, blue.b - noise, 1.0);
					}
				  </script>
				  <script id="vertexNoiseShader" type="x-shader/x-vertex">
						varying vec2 vUv;

						void main() {
						  vUv = uv;
						  gl_Position = projectionMatrix*modelViewMatrix*vec4(position, 1.0);
						}
				  </script>
				  <script id="fragmentNoiseShader" type="x-shader/x-fragment">
					  uniform vec2 u_resolution;
					  uniform float u_time;
					  uniform float u_amount;

					  varying vec2 vUv;

					  float random2(vec2 st) {
						  return fract(sin(dot(st.xy ,vec2(12.9898,78.233))) * 43758.5453);
					  }

					  void main() {
						// armazena um sistema de coordenadas normalizado
						vec2 st = gl_FragCoord.xy/u_resolution.xy;

						// multiplica a escala do sistema de cordenadas por u_amount
						st *= u_amount;

						// pega as coordenadas inteiras do sistema para criar um grid
						vec2 ipos = floor(st);

						// atribui um valor aleatório com base nas coordenadas inteiras
						vec3 noise = vec3(random2(ipos*u_time));

						gl_FragColor = vec4(noise, 1.0);
					}
				  </script>
					<script src="js/dat.gui.min.js"></script>
				  	<script src="./setup.js"></script>
					<script src="./objects.js"></script>
					<script src="./shaders.js"></script>
					<script src="./controllers.js"></script>
					<script src="./mouse_actions.js"></script>
					<script src="./animation.js"></script>
			</div>
	</body>
</html>
